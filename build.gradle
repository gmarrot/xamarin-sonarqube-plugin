plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
}

group 'com.betomorrow.gradle'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    notSnapshot = { !version.endsWith("SNAPSHOT") }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compileOnly gradleApi()

    // Third Party
    compile 'com.betomorrow.gradle:xamarin-build-tools:1.9.0'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    testCompile 'org.assertj:assertj-core:3.11.1'
    testCompile gradleTestKit()

    testCompile 'org.mockito:mockito-core:3.0.0'
    testCompile 'org.mockito:mockito-junit-jupiter:3.0.0'

    testCompile 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'

    testRuntime "cglib:cglib-nodep:3.2.4"       // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.4"   // allows mocking of classes without default constructor (together with CGLIB)
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}


/**
 * Integration Tests
 */

sourceSets {
    integrationTest {
        java.srcDir file('src/integration-test/java')
        kotlin.srcDir file('src/integration-test/kotlin')
        resources.srcDir file('src/integration-test/resources')

        compileClasspath += sourceSets.main.output + configurations.integrationTestCompile
        runtimeClasspath += output + compileClasspath + configurations.integrationTestRuntime
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompile.extendsFrom testCompile
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

compileIntegrationTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task integrationTest(type: Test, group: JavaBasePlugin.VERIFICATION_GROUP) {
    description = 'Run the integration tests.'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    outputs.upToDateWhen { false }
    mustRunAfter test

    useJUnitPlatform()
}

check.dependsOn integrationTest

gradlePlugin {
    testSourceSets sourceSets.integrationTest
}


/**
 * Publishing
 */

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

pluginBundle {
    website = 'https://github.com/gmarrot/xamarin-sonarqube-plugin'
    vcsUrl = 'https://github.com/gmarrot/xamarin-sonarqube-plugin'

    tags = ['xamarin', 'sonarqube', 'plugin', 'msbuild', 'nuget', 'sonar-scanner']

    plugins {
        sonarqubePlugin {
            id = 'com.betomorrow.xamarin.sonarqube'
            displayName = 'Gradle Plugin for SonarQube Scanner for MSBuild'
            description = 'Plugin to execute SonarQube Scanner for MSBuild in .Net Projects.'
        }
    }
}

publishPlugins {
    onlyIf notSnapshot
}


/**
 * Gradle Wrapper
 */

wrapper {
    gradleVersion = "5.4.1"
    distributionType = Wrapper.DistributionType.ALL
}